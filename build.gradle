buildscript {
    ext {
        springBootVersion = '3.4.1'
        queryDslVersion = "5.0.0"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:1.1.7.RELEASE")
    }
}

subprojects  {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'io.github.zuneho'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(23)
        }
    }
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-mustache'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-web'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        developmentOnly 'org.springframework.boot:spring-boot-devtools'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':sandbox-core') {
    bootJar { enabled = false }
    jar { enabled = true }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
        implementation "com.querydsl:querydsl-apt:${queryDslVersion}"

        //WebClient
        implementation 'org.springframework.boot:spring-boot-starter-webflux'


        implementation 'org.apache.poi:poi-ooxml:5.2.2'
        implementation 'commons-io:commons-io:2.18.0'
        implementation 'org.apache.commons:commons-lang3:3.17.0'
    }
}

project(':sandbox-api') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        runtimeOnly 'com.mysql:mysql-connector-j'
        implementation 'com.zaxxer:HikariCP'
        implementation project(':sandbox-core')

        developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    }
}